# Maintainer: Axilleas Pipinellis <axilleas@archlinux.info>

pkgname=kamaki
pkgver=0.7.7
pkgrel=1
pkgdesc="A command-line tool for managing clouds"
arch=(any)
url="http://code.grnet.gr/projects/kamaki"
license=('BSD')
changelog=Changelog
depends=('python2-objpool' 'python2-argparse')
makedepends=('python2-distribute' 'python2-sphinx')
optdepends=('python2-ansicolors: for color support' 
            'python2-progress>=1.0.2: for progress bar support')
source=("https://code.grnet.gr/attachments/download/2428/$pkgname-$pkgver.tar.gz")

package() {

    cd "$srcdir/$pkgname-$pkgver"
    python2 setup.py install --root="$pkgdir" -O1
    install -Dm644 COPYRIGHT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
    install -Dm644 Changelog "$pkgdir/usr/share/$pkgname/ChangeLog"

    # Build documentation and man page
    cd "$srcdir/$pkgname-$pkgver/docs"
    sed -i 's/sphinx-build/sphinx-build2/' Makefile
    make html && make man && rm -rf "_build/html/man" && rm -rf  _build/doctrees
    
    mkdir -p "$pkgdir"/usr/share/{man/man1,docs/"$pkgname"}
    install -Dm644 _build/man/*.1 "$pkgdir/usr/share/man/man1/"
    cp -r _build/html/ "$pkgdir/usr/share/docs/$pkgname/html"
    
}

# vim:set ts=2 sw=2 et:

md5sums=('9ff6ed5b7d6d1b9c6387f6a629534ea0')
